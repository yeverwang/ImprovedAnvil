/// BG2EE-specific patches ///

// DD, defensive stance
COPY_EXISTING ~spdwd02.spl~ ~override/spdwd02.spl~
  SAY UNIDENTIFIED_DESC @10216

  // reduce DR bonus
  PATCH_FOR_EACH ~opcode_number~ IN ~86~ ~87~ ~88~ ~89~ BEGIN
    LPF ~ALTER_SPELL_EFFECT~ INT_VAR
      check_headers = 1
      header_type = 1
      match_opcode = ~%opcode_number%~
      parameter1 = 25
    END
  END

  // add protection from Hardiness for the duration of the spell
  PATCH_FOR_EACH ~res~ IN ~SPCL907~ ~SPWISH12~ BEGIN
    LPF ~ADD_SPELL_EFFECT~
      INT_VAR
        opcode = 206
        target = 1
        timing = 0
        resist_dispel = 2
        duration = 60
      STR_VAR
        resource = EVALUATE_BUFFER ~%res%~
    END
  END

  BUT_ONLY

// In EE, II already has a +4 bonus to saving-throws embedded into the opcode
// therefore, remove the extra bonuses from the spell file
COPY_EXISTING ~SPWI405.spl~ ~override/SPWI405.spl~
  PATCH_FOR_EACH ~opcode_number~ IN ~33~ ~34~ ~35~ ~36~ ~37~ BEGIN
    LPF ~DELETE_SPELL_EFFECT~ INT_VAR
      opcode_to_delete = ~%opcode_number%~
    END
  END
  BUT_ONLY

// Favored of the Spirits, Shaman's HLA
COPY_EXISTING ~SPCL941.spl~ ~override/SPCL941.spl~
  SAY UNIDENTIFIED_DESC @10303

  WRITE_BYTE 0x27 13 // secondary type

  // reduce duration to two turns
  PATCH_FOR_EACH ~opcode_number~ IN ~208~ ~169~ ~267~ ~101~ ~142~ ~328~ ~232~ BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
      check_headers = 1
      header_type = 1
      match_opcode = ~%opcode_number%~
      duration = 120
    END
  END

  // add scripting state for AI's benefit
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 282
      target = 1
      timing = 0
      resist_dispel = 2
      duration = 120
      parameter1 = 1
      parameter2 = 1
  END

  // re-setup 174 opcodes (play sound) because we can't target delayed effect directly
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 174 END

  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 174
      target = 1
      timing = 1
      resist_dispel = 2
    STR_VAR
      resource = ~EFF_P84~
  END

  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 174
      target = 1
      timing = 4
      resist_dispel = 2
      duration = 120
    STR_VAR
      resource = ~EFF_E07~
  END

  BUT_ONLY

// Spirit Form, Shaman's HLA
COPY_EXISTING ~SPCL940.spl~ ~override/SPCL940.spl~
  // normal invisibility
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 20
      target = 1
      timing = 0
      resist_dispel = 0
      duration = 30
      parameter2 = 0
  END

  // immunity to II and Mass Inv.
  PATCH_FOR_EACH ~res~ IN ~SPWI721~ ~SPWI405~ BEGIN
    LPF ADD_SPELL_EFFECT
      INT_VAR
        opcode = 206
        target = 1
        timing = 0
        resist_dispel = 0
        duration = 30
      STR_VAR
        resource = EVALUATE_BUFFER ~%res%~
    END
  END

  // immunity to detect invisibility
  PATCH_FOR_EACH ~effect~ IN ~116~ ~220~ BEGIN
    LPF ADD_SPELL_EFFECT
      INT_VAR
        opcode = 101
        target = 1
        timing = 0
        resist_dispel = 0
        duration = 30
        parameter2 = effect
    END
  END

  // Protection from Divination spells
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 204
      target = 1
      timing = 0
      resist_dispel = 0
      duration = 30
      parameter2 = 3
  END

  BUT_ONLY

// Ethereal Retribution, Shaman's HLA
COPY_EXISTING ~SPPR751.spl~ ~override/SPPR751.spl~
  SAY UNIDENTIFIED_DESC @10343

  // update opcode to bypass resistance
  PATCH_FOR_EACH ~opcode~ IN ~12~ ~215~ ~44~ ~139~ ~142~ ~50~ ~174~ BEGIN
    LPF ALTER_SPELL_EFFECT INT_VAR
      check_headers = 1
      match_opcode = ~%opcode%~
      resist_dispel = 3
    END
  END

  // change STR drain to THAC0 drain
  LPF ALTER_SPELL_EFFECT INT_VAR
    check_headers = 1
    match_opcode = 44
    new_opcode = 278
  END

  // change displayed string on THAC0 drain
  LPF ALTER_SPELL_EFFECT INT_VAR
    check_headers = 1
    match_opcode = 139
    parameter1 = 8320
  END

  BUT_ONLY

// Aura of Despair, Blackguard's special ability
COPY_EXISTING ~SPCL103.spl~ ~override/SPCL103.spl~
  SAY UNIDENTIFIED_DESC @10316

  LPF ~ALTER_SPELL_EFFECT~ INT_VAR
    check_headers = 1
    header_type = 3
    savingthrow = 1
  END

  BUT_ONLY

// Flaming Fists, the monk kit ability
COPY_EXISTING ~SPCL238.spl~ ~override/SPCL238.spl~
  LPF ALTER_SPELL_EFFECT INT_VAR
    check_headers = 1
    header_type = 3
    match_opcode = 142
    resist_dispel = 3
  END

  BUT_ONLY
