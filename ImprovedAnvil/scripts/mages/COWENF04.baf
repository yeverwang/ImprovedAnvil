// Mage level 14 //
// First level spells (5): Chromatic orb x5 //
// Second level spells (5): Vocalize, Mirror Image, Acid Arrow x3 //
// Third level spells (5): Pro normal missile, v.t or Flame arrow x4 //
// Fourth level spells (4): Stoneskin, Minor Globe, Fire shiled, Spider Spawn //
// Fifth level spells (4): Spell immunity, oracle, Breach x2 //
// Sixth level spells (2): chain lightning, Imp. Haste //
// Seventh level spells (1): spell turning //
// Scrolls: Emotion x2 //

IF
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  OR(4)
   CheckStatGT(Myself,0,SCRIPTINGSTATE9)
   StateCheck(Myself,STATE_HELPLESS)
   StateCheck(Myself,STATE_STUNNED)
   Global("Iapcastts","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaspell","LOCALS",6)
END

IF
  Global("Iavocal","LOCALS",0)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    SetGlobal("Iavocal","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_VOCALIZE)
END

IF
  OR(2)
   Detect([PC])
   Detect([GOODCUTOFF])
  Global("Iacowled01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled01","LOCALS",1)
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
    ReallyForceSpell(Myself,WIZARD_MIRROR_IMAGE)
    ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_NORMAL_MISSILES)
    ReallyForceSpell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
    ReallyForceSpell(Myself,WIZARD_SPELL_TURNING)
    ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !StateCheck(Myself,STATE_SILENCED)
  Detect([PC])
  !See([PC])
  Global("Iacowled12","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled12","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_ORACLE)
END

IF
  HPPercentLT(Myself,100)
  !HPLT(Myself,1)
  See(NearestEnemyOf(Myself))
  Global("Iacowled01","LOCALS",1)
  CheckStatLT(Myself,1,STONESKINS)
THEN
  RESPONSE #100
    SetGlobal("Iacowled01","LOCALS",2)
    DisplayString(Myself,29736)  //
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
END

IF
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  Global("Iacowled05","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled05","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_SPIDER_SPAWN)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player1)
  !StateCheck(Player1,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player1,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  GlobalLT("Iacastbreach","LOCALS",2)
  Global("Iap1nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap1nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player1,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player2)
  !StateCheck(Player2,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player2,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  GlobalLT("Iacastbreach","LOCALS",2)
  Global("Iap2nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap2nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player2,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player3)
  !StateCheck(Player3,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player3,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  GlobalLT("Iacastbreach","LOCALS",2)
  Global("Iap3nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap3nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player3,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player4)
  !StateCheck(Player4,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player4,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  GlobalLT("Iacastbreach","LOCALS",2)
  Global("Iap4nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap4nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player4,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player5)
  !StateCheck(Player5,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player5,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  GlobalLT("Iacastbreach","LOCALS",2)
  Global("Iap5nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap5nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player5,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player6)
  !StateCheck(Player6,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player6,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  GlobalLT("Iacastbreach","LOCALS",2)
  Global("Iap6nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap6nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player6,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !StateCheck(Myself,STATE_HELPLESS)
  See([PC])
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_SLOWED)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  Global("Iacowled15","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled15","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    DisplayString(Myself,39969)  // ~Spell Sequencer - Fired~
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_GREATER_MALISON)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_CONFUSION)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_SLOW)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE8)
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  GlobalLT("Iascroll","LOCALS",2)
  HasItem("scrl5h",Myself) //
THEN
  RESPONSE #100
    IncrementGlobal("Iascroll","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    DisplayStringHead(Myself,46153) //
    UseItem("scrl5h",LastSeenBy(Myself))  //
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearest([PC]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE8)
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  GlobalLT("Iascroll","LOCALS",2)
  HasItem("scrl5h",Myself) //
THEN
  RESPONSE #100
    IncrementGlobal("Iascroll","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    DisplayStringHead(Myself,46153) //
    UseItem("scrl5h",LastSeenBy(Myself))  //
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(ThirdNearest([PC]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE8)
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
  GlobalLT("Iascroll","LOCALS",2)
  HasItem("scrl5h",Myself) //
THEN
  RESPONSE #100
    IncrementGlobal("Iascroll","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    DisplayStringHead(Myself,46153) //
    UseItem("scrl5h",LastSeenBy(Myself))  //
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  CheckStatLT(LastSeenBy(Myself),100,RESISTELECTRICITY)
  !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7)
  Global("Iacowled10","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled10","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  Global("Iacowled17","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled17","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_IMPROVED_HASTE)
END

IF
  !GlobalTimerNotExpired("Iatouch","LOCALS")
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  Range(LastSeenBy(Myself),4)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("Iacowled11","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled11","LOCALS",1)
    SetGlobalTimer("Iatouch","LOCALS",12)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_VAMPIRIC_TOUCH)
END

IF
  !GlobalTimerNotExpired("Iatouch","LOCALS")
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearestEnemyOf(Myself))
  Range(LastSeenBy(Myself),2)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("Iacowled11","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled11","LOCALS",1)
    SetGlobalTimer("Iatouch","LOCALS",12)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_VAMPIRIC_TOUCH)
END

IF
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaconjspl","LOCALS")
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iacowled11","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled11","LOCALS",1)
    SetGlobalTimer("Iaconjspl","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

IF
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaconjspl","LOCALS")
  See(SecondNearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iacowled11","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled11","LOCALS",1)
    SetGlobalTimer("Iaconjspl","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

IF
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaconjspl","LOCALS")
  See(SecondNearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTACID)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iacowled13","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled13","LOCALS",1)
    SetGlobalTimer("Iaconjspl","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaconjspl","LOCALS")
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTACID)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iacowled13","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled13","LOCALS",1)
    SetGlobalTimer("Iaconjspl","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTACID)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iacowled14","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled14","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_CHROMATIC_ORB)
END

IF
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearest([PC]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTACID)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iacowled14","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled14","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_CHROMATIC_ORB)
END

IF
  !StateCheck(Myself,STATE_SILENCED)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(ThirdNearest([PC]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTACID)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  !HasBounceEffects(LastSeenBy(Myself))
  GlobalLT("Iacowled14","LOCALS",4)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled14","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_CHROMATIC_ORB)
END

IF
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !Detect([PC])
  !Detect([GOODCUTOFF])
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  GlobalGT("Iacowled01","LOCALS",0)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  See(NearestEnemyOf(Myself))
  Global("Iacowled17","LOCALS",1)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackOneRound(NearestEnemyOf(Myself))
END