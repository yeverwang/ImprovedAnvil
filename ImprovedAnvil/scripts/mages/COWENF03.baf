// Mage Level 17 ////
// First level spells (5):  MM x5 //
// Second level spells (5): Vocalize, Mirror image, Blur, Acid arrow x2 //
// Third level spells (5): Remove Magic, v.t or Flame Arrow x3, MMM //
// Fourth level spells (5): Stoneskin, M Globe, Fire Shield, Emotion, Imp. Invis. //
// Fifth level spells (5): SI, Oracle x2, pro electric, Breach //
// Sixth level spells (3): Death Spell x2, Chain Lightning //
// Seventh level spells (3): Ruby Ray, Improved Mantle, Mordenkainen's sword //
// Eighth level spells (2): ADHW //
// Scrolls: Power word Silence //

IF
  Global("Iapcastts","GLOBAL",1)
  Global("Iatsinvulner","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",1)
    ApplySpellRES("SPINI70",Myself)
END

IF
  Global("Iapcastts","GLOBAL",0)
  Global("Iatsinvulner","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iatsinvulner","LOCALS",0)
    ApplySpellRES("SPINI71",Myself)
END

IF
  InMyArea(Player1)
  !See([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  OR(2)
   Detect([PC])
   Detect([GOODCUTOFF])
  Global("Iacowled01","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled01","LOCALS",1)
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
    ReallyForceSpell(Myself,WIZARD_BLUR)
    ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
    ReallyForceSpellRES("SPWII41",Myself)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_ELECTRICITY)
    ReallyForceSpell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
    ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_DIVINATION)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  OR(4)
   CheckStatGT(Myself,0,SCRIPTINGSTATE9)
   StateCheck(Myself,STATE_HELPLESS)
   StateCheck(Myself,STATE_STUNNED)
   Global("Iapcastts","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaspell","LOCALS",6)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_VOCALIZE)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOfType([0.0.0.0.0.ILLUSIONARY.0]))
  Global("Iaremove","LOCALS",0)
THEN
  RESPONSE #100
    IncrementGlobal("Iaremove","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_ORACLE)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !HPLT(Myself,1)
  !StateCheck(Myself,STATE_HELPLESS)
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),50,RESISTELECTRICITY)
  !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7)
  HPLT(Myself,50)
  Global("Iacowled06","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled06","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    DisplayString(Myself,39968) // Spell Trigger- Fired
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAIN_LIGHTNING)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  OR(2)
   StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
   StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("truesight","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("truesight","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_ORACLE)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !StateCheck(Myself,STATE_HELPLESS)
  See(NearestEnemyOf(Myself))
  StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE7)
  Global("Iaremove2","LOCALS",0)
THEN
  RESPONSE #100
    IncrementGlobal("Iaremove2","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    ForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
END

IF
  GlobalLT("truesight","LOCALS",2)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  Detect([PC])
  !See([PC])
THEN
  RESPONSE #100
    IncrementGlobal("truesight","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_ORACLE)
END

IF
  Allegiance(Myself,ENEMY)
  CheckStatLT(Myself,1,STONESKINS)
  Global("Iacowled05","LOCALS",0)
  !HPLT(Myself,1)
THEN
  RESPONSE #100
    SetGlobal("Iacowled05","LOCALS",1)
    DisplayString(Myself,29736) // Contingency - Stoneskin
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  CheckStatLT(Myself,1,STONESKINS)
  Global("Iacowled05","LOCALS",1)
  Global("Iacowled07","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled07","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_IMPROVED_MANTLE)
END

IF
  HPPercentLT(Myself,50)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  HasItem("potn52",Myself)  // ~Potion of Extra Healing~
THEN
  RESPONSE #100
    SetGlobalTimer("Iaspell","LOCALS",6)
    DisplayStringHead(Myself,46150)  // ~quaffs a potion~
    UseItem("potn52",Myself)  // ~Potion of Extra Healing~
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  Global("Iacowled08","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled08","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_MORDENKAINENS_SWORD)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player1)
  !StateCheck(Player1,STATE_IMPROVEDINVISIBILITY)
  Global("Iareversal","LOCALS",0)
  GlobalGT("Iap1nr","GLOBAL",0)
THEN
  RESPONSE #100
    IncrementGlobal("Iap1nr","GLOBAL",-1)
    IncrementGlobal("Iareversal","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player1,WIZARD_RUBY_RAY_OF_REVERSAL)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player1)
  !StateCheck(Player1,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player1,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  Global("Iacastbreach","LOCALS",0)
  Global("Iap1nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap1nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player1,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player2)
  !StateCheck(Player2,STATE_IMPROVEDINVISIBILITY)
  Global("Iareversal","LOCALS",0)
  GlobalGT("Iap2nr","GLOBAL",0)
THEN
  RESPONSE #100
    IncrementGlobal("Iap2nr","GLOBAL",-1)
    IncrementGlobal("Iareversal","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player2,WIZARD_RUBY_RAY_OF_REVERSAL)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player2)
  !StateCheck(Player2,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player2,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  Global("Iacastbreach","LOCALS",0)
  Global("Iap2nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap2nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player2,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player3)
  !StateCheck(Player3,STATE_IMPROVEDINVISIBILITY)
  Global("Iareversal","LOCALS",0)
  GlobalGT("Iap3nr","GLOBAL",0)
THEN
  RESPONSE #100
    IncrementGlobal("Iap3nr","GLOBAL",-1)
    IncrementGlobal("Iareversal","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player3,WIZARD_RUBY_RAY_OF_REVERSAL)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player3)
  !StateCheck(Player3,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player3,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  Global("Iacastbreach","LOCALS",0)
  Global("Iap3nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap3nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player3,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player4)
  !StateCheck(Player4,STATE_IMPROVEDINVISIBILITY)
  Global("Iareversal","LOCALS",0)
  GlobalGT("Iap4nr","GLOBAL",0)
THEN
  RESPONSE #100
    IncrementGlobal("Iap4nr","GLOBAL",-1)
    IncrementGlobal("Iareversal","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player4,WIZARD_RUBY_RAY_OF_REVERSAL)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player4)
  !StateCheck(Player4,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player4,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  Global("Iacastbreach","LOCALS",0)
  Global("Iap4nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap4nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player4,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player5)
  !StateCheck(Player5,STATE_IMPROVEDINVISIBILITY)
  Global("Iareversal","LOCALS",0)
  GlobalGT("Iap5nr","GLOBAL",0)
THEN
  RESPONSE #100
    IncrementGlobal("Iap5nr","GLOBAL",-1)
    IncrementGlobal("Iareversal","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player5,WIZARD_RUBY_RAY_OF_REVERSAL)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player5)
  !StateCheck(Player5,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player5,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  Global("Iacastbreach","LOCALS",0)
  Global("Iap5nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap5nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player5,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player6)
  !StateCheck(Player6,STATE_IMPROVEDINVISIBILITY)
  Global("Iareversal","LOCALS",0)
  GlobalGT("Iap6nr","GLOBAL",0)
THEN
  RESPONSE #100
    IncrementGlobal("Iap6nr","GLOBAL",-1)
    IncrementGlobal("Iareversal","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player6,WIZARD_RUBY_RAY_OF_REVERSAL)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(Player6)
  !StateCheck(Player6,STATE_IMPROVEDINVISIBILITY)
  !CheckStat(Player6,1,SCRIPTINGSTATE7)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_TURNING)
  !CheckSpellState(LastSeenBy(Myself),WIZARD_SPELL_SHIELD)
  Global("Iacastbreach","LOCALS",0)
  Global("Iap6nb","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("Iacastbreach","LOCALS",1)
    SetGlobal("Iap6nb","GLOBAL",0)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Player6,WIZARD_BREACH)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  HasItem("scrl7p",Myself)
  Global("Iascroll","LOCALS",0)
  Global("Iacowled13","LOCALS",0)
  See(NearestEnemyOfType([0.0.0.CLERIC_ALL]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !HasItem("amul21",LastSeenBy(Myself))
THEN
  RESPONSE #100
    SetGlobal("Iascroll","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    DisplayStringHead(Myself,46153) //
    UseItem("scrl7p",LastSeenBy(Myself)) //
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE7)
  Global("Iaremove3","LOCALS",0)
THEN
  RESPONSE #100
    IncrementGlobal("Iaremove3","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearest([PC]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  Global("Iacowled11","LOCALS",0)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE8)
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
THEN
  RESPONSE #100
    SetGlobal("Iacowled11","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_EMOTION_HOPELESSNESS)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  Global("Iacowled11","LOCALS",0)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
  !CheckStat(LastSeenBy(Myself),1,SCRIPTINGSTATE4)
  !CheckStatGT(LastSeenBy(Myself),0,SCRIPTINGSTATE8)
  !CheckStat(LastSeenBy(Myself),3,SCRIPTINGSTATE7)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE3)
THEN
  RESPONSE #100
    SetGlobal("Iacowled11","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_EMOTION_HOPELESSNESS)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  GlobalLT("Iacowled02","LOCALS",2)
  See(NearestEnemyOfType([GOODCUTOFF.0.0.0.0.SUMMONED]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !StateCheck(LastSeenBy(Myself),STATE_HELPLESS)
  !StateCheck(LastSeenBy(Myself),STATE_CONFUSED)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled02","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_DEATH_SPELL)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See([PC])
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE7)
  Global("Iacowled03","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled03","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
  Allegiance(Myself,ENEMY)
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearest([PC]))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE7)
  Global("Iacowled03","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled03","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
  !GlobalTimerNotExpired("Iatouch","LOCALS")
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  Range(LastSeenBy(Myself),4)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("Iacowled09","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled09","LOCALS",1)
    SetGlobalTimer("Iatouch","LOCALS",12)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_VAMPIRIC_TOUCH)
END

IF
  !GlobalTimerNotExpired("Iatouch","LOCALS")
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearestEnemyOf(Myself))
  Range(LastSeenBy(Myself),2)
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  !CheckStat(LastSeenBy(Myself),5,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("Iacowled09","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled09","LOCALS",1)
    SetGlobalTimer("Iatouch","LOCALS",12)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_VAMPIRIC_TOUCH)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaconjspl","LOCALS")
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("Iacowled09","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled09","LOCALS",1)
    SetGlobalTimer("Iaconjspl","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaconjspl","LOCALS")
  See(SecondNearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("Iacowled09","LOCALS",2)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled09","LOCALS",1)
    SetGlobalTimer("Iaconjspl","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_FLAME_ARROW)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",0)
  Allegiance(Myself,ENEMY)
  Global("Iacowled01","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobalTimer("chase","LOCALS",60)
    SetGlobal("partyfled","LOCALS",1)
END

IF
  !StateCheck(Myself,STATE_HELPLESS)
  !StateCheck(Myself,STATE_CONFUSED)
  !StateCheck(Myself,STATE_STUNNED)
  !StateCheck(Myself,STATE_PANIC)
  !StateCheck(Myself,STATE_CHARMED)
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Global("partyfled","LOCALS",1)
  GlobalTimerNotExpired("chase","LOCALS")
  InMyArea(Player1)
  Allegiance(Myself,ENEMY)
  Global("Iacowled01","LOCALS",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  !Detect([PC])
  !Detect([GOODCUTOFF])
  Allegiance(Myself,ENEMY)
  GlobalTimerExpired("chase","LOCALS")
  Global("partyfled","LOCALS",1)
  Global("Iacowled01","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("Iacowled01","LOCALS",0)
    SetGlobal("Iacowled02","LOCALS",0)
    SetGlobal("Iacowled03","LOCALS",0)
    SetGlobal("Iacowled04","LOCALS",0)
    SetGlobal("Iacowled05","LOCALS",0)
    SetGlobal("Iacowled07","LOCALS",0)
    SetGlobal("Iacowled08","LOCALS",0)
    SetGlobal("Iacowled09","LOCALS",0)
    SetGlobal("Iacowled10","LOCALS",0)
    SetGlobal("Iacastbreach","LOCALS",0)
    SetGlobal("Iacowled12","LOCALS",0)
    SetGlobal("truesight","LOCALS",0)
    SetGlobal("Iaremove","LOCALS",0)
    SetGlobal("Iaremove2","LOCALS",0)
    SetGlobal("Iaremove3","LOCALS",0)
    SetGlobal("Iacastbreach","LOCALS",0)
    ReallyForceSpellRES("SPWII07",Myself)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaconjspl","LOCALS")
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTACID)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("Iacowled10","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled10","LOCALS",1)
    SetGlobalTimer("Iaconjspl","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  !GlobalTimerNotExpired("Iaconjspl","LOCALS")
  See(SecondNearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,RESISTACID)
  CheckStatLT(LastSeenBy(Myself),90,RESISTMAGIC)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  !CheckStat(LastSeenBy(Myself),2,SCRIPTINGSTATE7)
  !StateCheck(LastSeenBy(Myself),STATE_MIRRORIMAGE)
  GlobalLT("Iacowled10","LOCALS",3)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled10","LOCALS",1)
    SetGlobalTimer("Iaconjspl","LOCALS",18)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_MELF_ACID_ARROW)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),100,RESISTMAGIC)
  !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7)
  !HasItemEquiped("morsword",LastSeenBy(Myself))
  GlobalLT("Iacowled12","LOCALS",5)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled12","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(SecondNearestEnemyOf(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_IMPROVEDINVISIBILITY)
  CheckStatLT(LastSeenBy(Myself),100,MAGICDAMAGERESISTANCE)
  !CheckStatGT(LastSeenBy(Myself),0,MINORGLOBE)
  CheckStatLT(LastSeenBy(Myself),100,RESISTMAGIC)
  !CheckStat(LastSeenBy(Myself),4,SCRIPTINGSTATE7)
  !HasItemEquiped("morsword",LastSeenBy(Myself))
  GlobalLT("Iacowled12","LOCALS",5)
THEN
  RESPONSE #100
    IncrementGlobal("Iacowled12","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(LastSeenBy(Myself),WIZARD_MAGIC_MISSILE)
END

IF
  !GlobalTimerNotExpired("Iaspell","LOCALS")
  See(NearestEnemyOf(Myself))
  Global("Iacowled13","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("Iacowled13","LOCALS",1)
    SetGlobalTimer("Iaspell","LOCALS",6)
    SpellNoDec(Myself,WIZARD_MELF_METEOR)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iacowled13","LOCALS",1)
  HasItem("melfmet",Myself)
  See(NearestEnemyOf(Myself))
  !Range(LastSeenBy(Myself),3)
  CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iacowled13","LOCALS",1)
  HasItem("melfmet",Myself)
  See(FourthNearestEnemyOf(Myself))
  CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iacowled13","LOCALS",1)
  HasItem("melfmet",Myself)
  See(ThirdNearestEnemyOf(Myself))
  CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iacowled13","LOCALS",1)
  HasItem("melfmet",Myself)
  See(SecondNearestEnemyOf(Myself))
  CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Allegiance(Myself,ENEMY)
  Global("Iacowled13","LOCALS",1)
  HasItem("melfmet",Myself)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  Global("Iacowled13","LOCALS",1)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackOneRound(NearestEnemyOf(Myself))
END